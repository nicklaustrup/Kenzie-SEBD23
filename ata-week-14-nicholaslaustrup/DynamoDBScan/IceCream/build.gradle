import groovy.xml.XmlSlurper

plugins {
    id 'ata-curriculum.snippets-conventions'
}

checkstyle {
    def suppressionFile = rootProject.file('Utilities/ATACheckstyle/configuration/checkstyle/unit1/suppressions.xml')
    configProperties.put('checkstyle.suppression.filter', suppressionFile)
}

dependencies {
    implementation 'javax.inject:javax.inject:1'
    implementation 'com.google.dagger:hilt-compiler:2.41'
    implementation 'com.google.dagger:dagger:2.30'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.30'
    testAnnotationProcessor 'com.google.dagger:hilt-compiler:2.41'
    implementation 'com.amazonaws:aws-java-sdk:1.12.162'
}

def wflowToTestsMap = [
        'dynamodbscan-icecream-test-phase-0': [
            'com.kenzie.dynamodbscan.icecream.Phase0Test'
        ],
        'dynamodbscan-icecream-test-phase-1': [
            'com.kenzie.dynamodbscan.icecream.Phase1Test'
        ],
        'dynamodbscan-icecream-test-phase-2': [
            'com.kenzie.dynamodbscan.icecream.Phase2Test'
        ],
        'dynamodbscan-icecream-test-phase-3': [
            'com.kenzie.dynamodbscan.icecream.Phase3Test'
        ],
        'dynamodbscan-icecream-all-tests': [
                'com.kenzie.dynamodbscan.icecream.Phase0Test',
                'com.kenzie.dynamodbscan.icecream.Phase1Test',
                'com.kenzie.dynamodbscan.icecream.Phase2Test',
                'com.kenzie.dynamodbscan.icecream.Phase3Test'
        ]
]

wflowToTestsMap.forEach { wflowName, testList ->
    tasks.register(wflowName) {
        dependsOn wflowtest
        test.doFirst {
            filter {
                testList.each { includeTestsMatching it }
            }
        }
        test.finalizedBy(copyTestResults)
    }
}

tasks.register('copyTestResults', Copy) {
    from(layout.buildDirectory.dir("test-results")) {
        include "test/TEST-com.kenzie.dynamodbscan.icecream.Phase0Test.xml"
        include "test/TEST-com.kenzie.dynamodbscan.icecream.Phase1Test.xml"
        include "test/TEST-com.kenzie.dynamodbscan.icecream.Phase2Test.xml"
        include "test/TEST-com.kenzie.dynamodbscan.icecream.Phase3Test.xml"
    }
    into layout.projectDirectory.dir("test-results")
}

def integrationResults = ["test/TEST-com.kenzie.dynamodbscan.icecream.Phase0Test.xml",
                          "test/TEST-com.kenzie.dynamodbscan.icecream.Phase1Test.xml",
                          "test/TEST-com.kenzie.dynamodbscan.icecream.Phase2Test.xml",
                          "test/TEST-com.kenzie.dynamodbscan.icecream.Phase3Test.xml"]

tasks.register('verifytests-icecream') {
    dependsOn tasks.copyTestResults
    doLast {
        integrationResults.forEach {
            def testResult = layout.projectDirectory.dir("test-results").file(it).asFile
            def attributesMap = new XmlSlurper().parse(testResult).attributes()
            assert attributesMap.get("skipped") == "0"
            assert attributesMap.get("failures") == "0"
            assert attributesMap.get("errors") == "0"
        }
    }
}
